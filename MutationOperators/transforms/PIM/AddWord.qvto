import m2m.qvt.oml.ExampleJavaLib;

modeltype PIM uses PIM('http://benchmarkdp.datagenerator.model.PIM');
	
transformation AddWord(inout model:PIM);
configuration property numWords:String;

main() {
	var paragraphs := model.rootObjects()[Document].pages.elements->selectByType(Paragraph);
	var wc := 0;
	if (numWords<>null) {
		wc := stringToInt(numWords);
	} else {
		var rn := getRandomReal();
		switch {
			case (rn < 0.02) wc := getRandomBetween(1,100); 
			case (rn >= 0.02 and rn < 0.42) wc := getRandomBetween(101,1000);
			case (rn >= 0.42 and rn < 0.87) wc := getRandomBetween(1001,5000);
			case (rn >= 0.87 and rn < 1) wc := getRandomBetween(5001,10000);
			else null; 
		};	
	};
	
	if (paragraphs->size() > 0) {
		
		// put at least one word to each paragraph
		var i:= 1;
		while (i <= paragraphs->size() and wc > 0) {
			var paragraph := paragraphs->asSequence()->at(i);
			paragraph->map AddWord(wc);
			wc := wc - 1;		
			i := i + 1;
		};
	
		// put remaining words randomly 
		while (wc > 0) {
			var pos := getRandom(paragraphs->size());
			var paragraph := paragraphs->asSequence()->at(pos);
			paragraph->map AddWord(wc);
			wc := wc - 1;		
		};	
	}
	
}

mapping inout PIM::Paragraph::AddWord(inout i:Integer) {
	var t := object Word {
		value := getRandomWord();
	}; 
	words += t;
}
